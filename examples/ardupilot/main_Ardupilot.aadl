package Main_Ardupilot
public
  with Boards::Ardupilot;
  with Ardupilot_Software;
  with Ardupilot_platform;
  with devices::EM406A;
  with devices::MPXV7002DP;
  with devices::XYZ_IR;
  with shields::Ardupilot;
  with Deployment;
  with data_sheet;

  ---------------
  -- Ardupilot --
  ---------------

  system Ardupilot_Shield
  end Ardupilot_Shield;

  system implementation Ardupilot_Shield.impl
  subcomponents
  Shield	: system shields::Ardupilot::Ardupilot;	-- Ardupilot Shield
  GPS		: device devices::EM406A::EM406A;	-- GPS module
  XYZ		: device devices::XYZ_IR::XYZ_IR;	-- Infrared Sensor

  Connections
  B1 : port XYZ.Out_XY -> Shield.XY_in;
  B2 : port XYZ.Out_Z -> Shield.Z_in;
  B3 : port GPS.PPS -> Shield.ada_gps;
  end Ardupilot_Shield.impl;


  system Ardupilot_Map extends Boards::Ardupilot::Ardupilot
  Properties
  Data_Sheet::UUID => "http://code.google.com/p/ardupilot/wiki/AirFrame";
  end Ardupilot_Map;

  system implementation Ardupilot_Map.impl extends Boards::Ardupilot::Ardupilot.impl
  subcomponents
   process_Ardupilot : process Ardupilot_software::Ardupilot_code.impl;
   Ardupilot: system Boards::Ardupilot::Ardupilot.impl;
   ESC		: device Ardupilot_platform::ESC;	-- electronic speed control
   Battery	: device Ardupilot_platform::Battery;-- supply battery
   aileron	: device Ardupilot_platform::aileron;-- hinged flight control surfaces
   RC_Rx	: device Ardupilot_platform::RC_Rx;	-- receiver channel
   elevator	: device Ardupilot_platform::elevator; -- elevator servo
   Motor	: device Ardupilot_platform::Motor;
  Connections
  -- RC Rx / Ardupilot
  Cx1 : port RC_Rx.ch1 -> Ardupilot.IN1;
  Cx2 : port RC_Rx.ch2 -> Ardupilot.IN2;
  Cx3 : port RC_Rx.ch3 -> Ardupilot.IN3;
  Cx4 : port RC_Rx.ch5 -> Ardupilot.CTRL;

  -- servos, battery /Ardupilot
  Cx5 : port Ardupilot.out1 -> aileron.ail;
  Cx6 : port Ardupilot.out2 -> elevator.ele;
  Cx7 : port Ardupilot.out3 -> ESC.pin;

  -- between components
  Cx8 : port ESC.outM -> Motor.pin;
  Cx9 : port battery.vout -> ESC.Vin;

  properties
    Actual_Processor_Binding => (reference (ATM328)) applies to process_Ardupilot;

  end Ardupilot_Map.impl;

end Main_Ardupilot;
