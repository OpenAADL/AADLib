----------------------------------------------------------------------------------------
-- Compute for each bus the consumption of all devices connected (ignore regulator capabilities)
 
theorem Power_Consumption

foreach e in Bus_Set do

   Cnx_Set (e) := {x in Connection_Set | (Property_Exists (e,"Electricity::Bus_Voltage_Range")) and (Is_Accessing_To (x,e)) };

   Connected_Device := { d in Device_Set |  (Property_Exists (e,"Electricity::Bus_Voltage_Range")) and (Is_Accessed_By (d, Cnx_Set))
   };

   var Min_Power_Level := Min(Get_Property_Value(Connected_Device,"Deployment::Power_Level"));

   Power_Supplier := { d in Connected_Device | (Property_Exists (d,"Deployment::Power_Level")) and (Get_Property_Value(d,"Deployment::Power_Level") <= Min_Power_Level)
   };
  
   -- includes regulators and normal devices (but not processors)
   Power_Device_Consumers := { d in Connected_Device | (Property_Exists (d, "Deployment::Power_Level")) and (Get_Property_Value(d,"Deployment::Power_Level") > Min_Power_Level)
   };

   var max_bus_voltage := (Last(Get_Property_Value(e,"Electricity::Bus_Voltage_Range")));
   var device_max_current := sum(Get_Property_Value (Power_Device_Consumers, "Electricity::Run_Current"));
   var power_consumption := device_max_current * max_bus_voltage;
   return(MSum(power_consumption));

end Power_Consumption;

----------------------------------------------------------------------------------------
-- Check that the total max consumption (in all power buses) does not exceed max battery power
 
theorem Battery_Sizing

foreach e in System_Set do

   Battery_Device := { d in Device_Set | (Get_Property_Value(d,"Deployment::Power_Level") = 0)
   };

   var pwr_consumption :=  compute Power_Consumption;
   var max_batteries_current := sum(Get_Property_Value(Battery_Device, "Electricity::Max_Current"));
   var batteries_voltage := sum(Get_Property_Value(Battery_Device, "Electricity::Core_Voltage"));
   var max_batteries_power := max_batteries_current * batteries_voltage;

   check(pwr_consumption <= max_batteries_power);

end Battery_Sizing;

