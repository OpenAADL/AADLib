package minirocket_system
public 
  with Deployment;
  with buses;
  with devices::pressure_hca0811;
  with devices::gps_lea4h;
  with devices::inertial_chr6d;
  with devices::camera_usb;
  with minirocket_sw;
  with Buses::I2C;
  with Buses::UART;
  with Buses::USB;
  with Processors::ARM;

  ------------
  -- System --
  ------------

  system minirocket 
  end minirocket;

  system implementation minirocket.impl
  subcomponents
    ------------------------------------------------------------------
    -- Node: hardware components : CCDS (ARM9 card)
    ------------------------------------------------------------------
    -- CCDS Interconnect bus. This component are shared by ARM9 peripherals and
    -- sensors/actautors to support communication through various protocols
    ------------------------------------------------------------------
    I2C_bus    : bus Buses::I2C::I2C.impl;
    USB1_bus   : bus buses::USB::USB.impl;
    UART1_bus  : bus buses::UART::UART.impl;
    UART2_bus  : bus buses::UART::UART.impl;
    ------------------------------------------------------------------
    --  Hardware components for CCDS configuration
    ------------------------------------------------------------------
    proc_2    : process    minirocket_sw::proc_2.impl;
    CPU_ARM9  : processor  Processors::ARM::AT91SAM9G20.impl;
    ------------------------------------------------------------------
    --  Processor peripherals for CCDS communication with sensors
    ------------------------------------------------------------------
    TWI_ARM9   :      device     Processors::ARM::TWI_Peripheral;  
    UART1_ARM9 :      device     Processors::ARM::USART1_Peripheral; 
    UART2_ARM9 :      device     Processors::ARM::USART2_Peripheral; 
    USB1_ARM9  :       device     Processors::ARM::USB1_Peripheral; 
    ------------------------------------------------------------------
    -- End Node
    ------------------------------------------------------------------

   ------------------------------------------------------------------
   --  List of sensors/actuators
   ------------------------------------------------------------------
    --------------------- ARM7 ----------------------
    PRESSURE   :      device     devices::pressure_hca0811::PRESSURE.impl
    { Deployment::Location => "xxx";}; -- define here the address of the I2C device
    INERTIAL      :   device     devices::inertial_chr6d::INERTIAL.impl;
    GPS           :   device     devices::gps_lea4h::GPS.impl;
    CAMERA        :   device     devices::camera_usb::CAMERA.impl;
    
  connections
    ------------------------------------------------------------------
    -- Node  bus connections 
    ------------------------------------------------------------------
    H1:bus access I2C_bus -> TWI_ARM9.Serial_Wire;
    H2:bus access I2C_bus -> PRESSURE.Serial_Wire;
  
    H3:bus access UART1_bus -> UART1_ARM9.Serial_Wire;
    H4:bus access UART1_bus -> GPS.Serial_Wire;
    
    H5:bus access UART2_bus -> UART2_ARM9.Serial_Wire;
    H6:bus access UART2_bus -> INERTIAL.Serial_Wire;
    --
    H7:bus access USB1_bus -> USB1_ARM9.Serial_Wire;
    H8:bus access USB1_bus -> CAMERA.Serial_Wire;
    
    
  properties
    Actual_Processor_Binding => (reference (CPU_ARM9)) applies to proc_2;
    --Actual_Processor_Binding => (reference (CPU_ARM7)) applies to I2C_Cnx;
    annex 
    real_specification {**
    	
    theorem Electricity_Prop_Defined
    foreach e in Device_Set do
    requires(Electricity_Prop_Defined);
    check(1=1);
    end Electricity_Prop_Defined;
    
    theorem Utilization_prop_def
    foreach e in Thread_Set do
    requires(Utilization_prop_def);
    check(1=1);
    end Utilization_prop_def;
    
  **};
  end minirocket.impl;

end  minirocket_system;
