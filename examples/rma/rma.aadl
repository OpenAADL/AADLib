--  This AADL model illustrates how to conduct schedulability analysis
--  using Cheddar, and then code generation of periodic tasks.
--
--  Two periodic tasks run in parrallel, without interaction. Tasks
--  parameters allows RMA analysis

package RMAAadl
public
  with Deployment;
  
  -----------------
  -- Subprograms --
  -----------------
  
  subprogram Hello_Spg_1
  properties
    source_language => (C);
    source_name     => "user_hello_spg_1";
    source_text     => ("hello.c");
  end Hello_Spg_1;
  
  subprogram Hello_Spg_2
  properties
    source_language => (C);
    source_name     => "user_hello_spg_2";
    source_text     => ("hello.c");
  end Hello_Spg_2;
  
  -------------
  -- Threads --
  -------------
  
  thread Task
  end Task;
  
  thread implementation Task.impl_1
  calls 
    Mycalls: {
    P_Spg : subprogram Hello_Spg_1;
    };
  properties
    Dispatch_Protocol                  => Periodic;
    Priority                           => 1;
    Period                             => 1000ms;
    Compute_Execution_time             => 0 ms .. 3 ms;
    Deadline                           => 1000 ms;
  end Task.impl_1;
  
  thread implementation Task.impl_2
  calls 
    Mycalls: {
    P_Spg : subprogram Hello_Spg_2;
    };
  properties
    Dispatch_Protocol                  => Periodic;
    Priority                           => 2;
    Period                             => 500ms;
    Compute_Execution_time             => 0 ms .. 3 ms;
    Deadline                           => 500 ms;
  end Task.impl_2;
  
  ---------------
  -- Processor --
  ---------------
  
  processor cpurm
  properties
    Deployment::Execution_Platform => Native;
  end cpurm;
  
  processor implementation cpurm.impl
  properties
    Scheduling_Protocol => (RMS);
  end cpurm.impl;

  ---------------
  -- Processes --
  ---------------
  
  process node_a
  end node_a;
  
  process implementation node_a.impl
  subcomponents
    Task1 : thread Task.impl_1;
    Task2 : thread Task.impl_2;
  end node_a.impl;
  
  ------------
  -- System --
  ------------
  
  system rma 
  end rma;
  
  system implementation rma.impl
  subcomponents
    node_a : process node_a.impl;
    cpu_rm : processor cpurm.impl;
  properties
    Actual_Processor_Binding => (reference (cpu_rm)) applies to node_a;
  end rma.impl;

end RMAAadl;
