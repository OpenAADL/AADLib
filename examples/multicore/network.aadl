package Network
public

  with Deployment;
  with Processor_Properties;
  with Bus_Properties;

  data configuration_type_ip
    properties
      Type_Source_Name  =>  "IP-Conf-T";
      Deployment::ASN1_Module_Name  =>  "POHICDRIVER-IP";
      Source_Language               =>  (ASN1);
      Source_Text =>  (
        "/usr/local/ocarina/include/ocarina/runtime/polyorb-hi-c/src/drivers/configuration/ip.asn",
        "drivers/configuration/ip.h");
  end configuration_type_ip;

  subprogram spg_sockets_init
    properties
      Source_Language =>  (C);
      Source_Name     =>  "__po_hi_driver_sockets_init";
  end spg_sockets_init;

  subprogram spg_l0_linux_sender
    properties
      Source_Language  => (C);
      Source_Name     =>  "__po_hi_driver_sockets_send";
  end spg_l0_linux_sender;

  subprogram implementation spg_l0_linux_sender.i
  end spg_l0_linux_sender.i;

  abstract sockets_impl
    properties
      Deployment::Configuration_Type => classifier(configuration_type_ip);
  end sockets_impl;

  abstract implementation sockets_impl.i
    subcomponents
      sender  : subprogram spg_l0_linux_sender.i;
  end sockets_impl.i;

  virtual bus pohi
  end pohi;

  virtual bus implementation pohi.c
  end pohi.c;

  device loopback
    features
      EBus  : requires bus access Ethernet.XPS8500;
  end loopback;

  device implementation loopback.XPS8500
    properties
      Deployment::Driver_Name =>  "sockets";
      Device_Driver =>  classifier(sockets_impl.i);
      Initialize_Entrypoint =>  classifier(spg_sockets_init);
      Provided_Virtual_Bus_Class  =>  (classifier(pohi.c));
  end loopback.XPS8500;

  bus Ethernet
    properties
--      Deployment::Transport_API =>  BSD_Sockets;
      Bus_Properties::Available_Bandwidth => (10 MBytesps, 100 MBytesps, 1 GBytesps);
  end Ethernet;

  bus implementation Ethernet.XPS8500
  end Ethernet.XPS8500;

end Network;