package AvionicsBusManagers
public
  with Deployment;
  with Processor_Properties;
  with Bus_Properties;
  
  with BusMgr0;
  with RTBusMgr0;
  with RTBusMgr1;
  with RTBusMgr2;
  with Network;
  
  -- System Definition
    
  virtual processor logical
  end logical;
  
  virtual processor implementation logical.core
    properties
      Deployment::Execution_Platform  =>  Native;
      Priority_Range                  =>  1 .. 255;
      Scheduling_Protocol             =>  (RMS);
  end logical.core;
  
  processor Intel
    features
      EBUS : requires bus access Network::Ethernet;
  end Intel;
  
  processor implementation Intel.i7_3770
    subcomponents
      Cpu0  : virtual processor logical.core  {Processor_Properties::Core_Id => 0;};
      Cpu1  : virtual processor logical.core  {Processor_Properties::Core_Id => 1;};
      Cpu2  : virtual processor logical.core  {Processor_Properties::Core_Id => 2;};
      Cpu3  : virtual processor logical.core  {Processor_Properties::Core_Id => 3;};
    properties
      Processor_Properties::Processor_Family    =>  x86;
      Processor_Properties::Endianess           =>  Little_Endian;
      Processor_Properties::Word_Length         =>  64 bits;
      Processor_Properties::Processor_Frequency =>  3400 MHz;
      Deployment::Supported_Runtime             =>  PolyORB_HI_C;
  end Intel.i7_3770;
  
  system AvionicsBusMgr
  end AvionicsBusMgr;

  system implementation AvionicsBusMgr.XPS8500
    subcomponents
      CPU   : processor Intel.i7_3770;
      B0BC  : process BusMgr0::BusControllerMgr.impl;
      M0RT  : process RTBusMgr0::M0RemoteTerminalMgr.impl;
      M1RT  : process RTBusMgr1::M1RemoteTerminalMgr.impl;  
      M2RT  : process RTBusMgr2::M2RemoteTerminalMgr.impl;
      EB    : bus Network::Ethernet.XPS8500;
      L0    : device Network::loopback.XPS8500 {Source_Text => ("devicesconf.c");};
      L1    : device Network::loopback.XPS8500 {Source_Text => ("devicesconf.c");};
      L2    : device Network::loopback.XPS8500 {Source_Text => ("devicesconf.c");};
      L3    : device Network::loopback.XPS8500 {Source_Text => ("devicesconf.c");};
    connections
      SC0   : bus access EB <-> L0.EBUS;
      SC1   : bus access EB <-> L1.EBUS;
      SC2   : bus access EB <-> L2.EBUS;
      SC3   : bus access EB <-> L3.EBUS;
      SC4   : bus access EB <-> CPU.EBus;
      SC5   : port  M0RT.RTtoBCMsg  ->  B0BC.M0RTtoBCMsg  {Actual_Connection_Binding => (reference(EB));};
      SC6   : port  M1RT.RTtoBCMsg  ->  B0BC.M1RTtoBCMsg  {Actual_Connection_Binding => (reference(EB));};
      SC7   : port  M2RT.RTtoBCMsg  ->  B0BC.M2RTtoBCMsg  {Actual_Connection_Binding => (reference(EB));};
    properties
     Actual_Processor_Binding => (reference(CPU.Cpu0)) applies to B0BC;
     Actual_Processor_Binding => (reference(CPU.Cpu1)) applies to M0RT;
     Actual_Processor_Binding => (reference(CPU.Cpu2)) applies to M1RT;
     Actual_Processor_Binding => (reference(CPU.Cpu3)) applies to M2RT;       
     Actual_Processor_Binding => (reference(CPU.Cpu0)) applies to L0; 
     Actual_Processor_Binding => (reference(CPU.Cpu1)) applies to L1; 
     Actual_Processor_Binding => (reference(CPU.Cpu2)) applies to L2; 
     Actual_Processor_Binding => (reference(CPU.Cpu3)) applies to L3; 
  end AvionicsBusMgr.XPS8500;
  
end AvionicsBusManagers;
